apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'AndResGuard'

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}
android {
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    compileSdkVersion 29

    defaultConfig {
        applicationId "cn.vove7.vassistant"
        minSdkVersion 21
        targetSdkVersion 29
        def subVersion = 3
        versionName "2.1.2"
        def vc = Integer.parseInt(versionName.replace(".", "")) * 10
        if (vc < 10000) {
            vc *= 10
        }
        versionCode vc + subVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        useLibrary 'org.apache.http.legacy'

        resValue "string", "app_name", "V Assistant"
        resValue "string", "base_acc_service_label", "V Assist基础服务"
        resValue "string", "gesture_acc_service_label", "V Assist高级服务（7.0+可用）"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(localProperties.getProperty('keyPath'))
            keyAlias = localProperties.getProperty('keyAlias')
            keyPassword localProperties.getProperty('keyPassword')
            storePassword localProperties.getProperty('storePassword')
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "armeabi-v7a"
            }
            signingConfig signingConfigs.release
        }
        release_x86 {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "armeabi-v7a", "x86"
            }
            signingConfig signingConfigs.release
        }
        release_ndk_all {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
            }
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix = '1'
            jniDebuggable = false
            debuggable = true
            resValue "string", "app_name", "V Assistant-debug"
            resValue "string", "base_acc_service_label", "V Assist基础DEBUG"
            resValue "string", "gesture_acc_service_label", "V Assist高级DEBUG"

            ndk {//排除其他 abi
                abiFilters "armeabi-v7a"
            }
            signingConfig signingConfigs.release
        }
        debug_x86 {
            applicationIdSuffix ".debug"
            versionNameSuffix = '1'
            jniDebuggable = false
            debuggable = true
            resValue "string", "app_name", "V Assistant-debug"
            resValue "string", "base_acc_service_label", "V Assist基础DEBUG"
            resValue "string", "gesture_acc_service_label", "V Assist高级DEBUG"

            ndk {//排除其他 abi
                abiFilters "armeabi-v7a", "x86"
            }
            signingConfig signingConfigs.release
        }
        debugWithMinify {
            applicationIdSuffix ".debug"
            versionNameSuffix = '1'
            jniDebuggable = false
            debuggable = true
            resValue "string", "app_name", "V Assistant-debug"
            resValue "string", "base_acc_service_label", "V Assist基础DEBUG"
            resValue "string", "gesture_acc_service_label", "V Assist高级DEBUG"

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'

            ndk {//排除其他 abi
                abiFilters "armeabi-v7a"
            }
            signingConfig signingConfigs.release
        }
//        beta {
//            versionNameSuffix = '-beta'
//        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        //最大堆内存
        javaMaxHeapSize "4g"
        //是否支持大工程模式
        jumboMode = true
        //预编译
        preDexLibraries = true
        //线程数
        threadCount = 8
    }
    productFlavors {
    }
    packagingOptions {
        exclude 'META-INF/proguard/coroutines.pro'
    }

    //配置打包apk路径及文件名
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (buildType.name != "debug") {
                def filename = "../../apks/vassist-v${versionCode}-${buildType.name}.apk"
                println("${buildType.name} -> $filename")
                outputFileName = filename
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/common_release.kotlin_module'
    }

}

task makeJar(type: Jar) {
    //指定生成的jar名
    baseName 'vassist-plugin-api2'
    //从哪里打包class文件
    from('build/tmp/kotlin-classes/debug/cn/vove7')
    //打包到jar后的目录结构
    into('cn/vove7/')
    //去掉不需要打包的目录和文件
    exclude('BuildConfig.class')
    //去掉R$开头的文件
    //exclude{ it.name.startsWith('R$');}
}

// 资源混淆
andResGuard {
    // mappingFile = file("./resource_mapping.txt")
    mappingFile = null
    use7zip = true
    useSign = true
    // 打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字
    keepRoot = false
    // 设置这个值，会把arsc name列混淆成相同的名字，减少string常量池的大小
    fixedResName = "arg"
    // 打开这个开关会合并所有哈希值相同的资源，但请不要过度依赖这个功能去除去冗余资源
    mergeDuplicatedRes = true
    whiteList = [
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.18'
        //path = "/usr/local/bin/7za"
    }

    /**
     * 可选： 如果不设置则会默认覆盖assemble输出的apk
     **/
    def date = new Date().format("[MM-dd]")
    println(date)
    finalApkBackupPath = "${project.rootDir}/apks/v-assist-${date}.apk"

    /**
     * 可选: 指定v1签名时生成jar文件的摘要算法
     * 默认值为“SHA-1”
     **/
    // digestalg = "SHA-256"
}
dependencies {
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.3.0')

    implementation "androidx.appcompat:appcompat:$androidx_version"
    implementation "androidx.constraintlayout:constraintlayout:2.0.1"
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'

    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation project(':executorengine')
    implementation project(path: ':common')
    implementation project(path: ':androlua')
    implementation project(path: ':rhino-android')
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:color:3.3.0'
    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    implementation 'org.adw.library:discrete-seekbar:1.0.1'
    implementation 'com.github.didikee:AndroidDonate:0.1.0'

    implementation 'com.github.Vove7:CodeView:1.0.2'
    implementation 'com.github.Vove7:BottomDialog:2.2.7'

    implementation 'com.github.Vove7:SlidePickerView:1.0.2'

    implementation 'com.github.SJJ-dot:Cockroach:2.0.1'

    implementation 'com.github.KeepSafe:TapTargetView:1.13.0'
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'
    implementation 'com.github.Vove7:recycler-fast-scroll:0.3.3'

    implementation 'com.king.zxing:zxing-lite:1.1.1'
    implementation 'com.github.Vove7.MarkdownView:library:0.20.1'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'

    implementation project(path: ':speechLib')
    implementation project(':admin-manager')

    implementation "com.github.infotech-group:android-drawable-dsl:0.3.0"
    def work_version = "2.4.0"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    implementation 'com.google.android:flexbox:2.0.0'

}
repositories {
    flatDir {
        dirs 'libs'
    }
}
