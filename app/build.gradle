apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.michaelrocks.paranoid'

android {
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    compileSdkVersion 30

    defaultConfig {
        applicationId "cn.vove7.vassistant"
        minSdkVersion 21
        targetSdkVersion 30
        def subVersion = 0
        versionName "2.3.9"
        def vc = Integer.parseInt(versionName.replace(".", "")) * 10
        if (vc < 10000) {
            vc *= 10
        }
        versionCode vc + subVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        useLibrary 'org.apache.http.legacy'

        resValue "string", "app_name", "V Assistant"
        resValue "string", "base_acc_service_label", "V Assist基础服务"
        resValue "string", "gesture_acc_service_label", "V Assist高级服务（7.0+可用）"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file('../temp.keystore')
            keyAlias = 'vassist'
            keyPassword '123456'
            storePassword '123456'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "armeabi-v7a"
            }
            signingConfig signingConfigs.release
        }
        release_x86 {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "armeabi-v7a", "x86"
            }
            signingConfig signingConfigs.release
        }
        release_ndk_all {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
            }
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix = '1'
            jniDebuggable = false
            debuggable = true
            resValue "string", "app_name", "V Assistant-debug"
            resValue "string", "base_acc_service_label", "V Assist基础DEBUG"
            resValue "string", "gesture_acc_service_label", "V Assist高级DEBUG"

//            ndk {//排除其他 abi
//                abiFilters "armeabi-v7a"
//            }
            signingConfig signingConfigs.release
        }
        debugWithMinify {
            applicationIdSuffix ".debug"
            versionNameSuffix = '1'
            jniDebuggable = false
            debuggable = true
            resValue "string", "app_name", "V Assistant-debug"
            resValue "string", "base_acc_service_label", "V Assist基础DEBUG"
            resValue "string", "gesture_acc_service_label", "V Assist高级DEBUG"

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'

            ndk {//排除其他 abi
                abiFilters "armeabi-v7a"
            }
            signingConfig signingConfigs.release
        }
//        beta {
//            versionNameSuffix = '-beta'
//        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
    packagingOptions {
        exclude 'META-INF/proguard/coroutines.pro'
    }

    //配置打包apk路径及文件名
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (buildType.name != "debug") {
                def filename = "../../apks/vassist-v${versionCode}-${buildType.name}.apk"
                println("${buildType.name} -> $filename")
                outputFileName = filename
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/common_release.kotlin_module'
    }

}

task makeJar(type: Jar) {
    //指定生成的jar名
    baseName 'vassist-plugin-api2'
    //从哪里打包class文件
    from('build/tmp/kotlin-classes/debug/cn/vove7')
    //打包到jar后的目录结构
    into('cn/vove7/')
    //去掉不需要打包的目录和文件
    exclude('BuildConfig.class')
    //去掉R$开头的文件
    //exclude{ it.name.startsWith('R$');}
}

dependencies {
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0')

    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.0"
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation project(':executorengine')
    implementation project(path: ':common')
    implementation project(path: ':jadb')
    implementation project(path: ':tts')
    implementation project(path: ':androlua')
    implementation project(path: ':rhino-android')
    implementation project(path: ':dui-speech-lib')

    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:color:3.3.0'
    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.3.0'
    implementation 'com.afollestad.material-dialogs:datetime:3.3.0'

    implementation 'org.adw.library:discrete-seekbar:1.0.1'
    implementation 'com.github.didikee:AndroidDonate:0.1.0'

    implementation 'com.github.Vove7:CodeView:1.0.2'
    implementation 'com.github.Vove7:BottomDialog:2.3.0'

    implementation 'com.github.Vove7:SlidePickerView:1.0.2'

    implementation('com.github.SJJ-dot:Cockroach:2.0.1') {
        exclude group: 'me.weishu', module: 'free_reflection'
    }

    implementation 'com.github.KeepSafe:TapTargetView:1.13.0'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
//    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.github.Vove7:recycler-fast-scroll:0.3.3'

    implementation 'com.github.Vove7.MarkdownView:library:0.20.1'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'

    implementation project(path: ':speechLib')

    implementation "com.github.infotech-group:android-drawable-dsl:0.3.0"
    def work_version = "2.5.0"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    implementation 'com.google.android:flexbox:2.0.1'

    implementation 'com.github.jenly1314:ZXingLite:2.1.0'

    compileOnly 'com.microsoft.design:fluent-system-icons:1.1.139'

    implementation 'com.github.tiann:epic:0.11.2@aar'
    implementation 'me.weishu.exposed:exposed-xposedapi:0.4.5'
    implementation 'com.github.tiann:FreeReflection:3.1.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'
}