apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "cn.vove7.vassistant"
        minSdkVersion 21
        targetSdkVersion 28
        def subVersion = 2
        versionName "2.1.0"
        def vc = Integer.parseInt(versionName.replace(".", "")) * 10
        if (vc < 10000) {
            vc *= 10
        }
        versionCode vc + subVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        useLibrary 'org.apache.http.legacy'

        resValue "string", "app_name", "V Assistant"
        resValue "string", "base_acc_service_label", "V Assist基础服务"
        resValue "string", "gesture_acc_service_label", "V Assist高级服务（7.0+可用）"

        manifestPlaceholders = [
                JPUSH_PKGNAME: "cn.vove7.vassistant",
                JPUSH_APPKEY : "f4b11a8374fbc79c3746a068", //JPush 上注册的包名对应的 Appkey.
                JPUSH_CHANNEL: "default", //暂时填写默认值即可.
        ]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "armeabi-v7a"
            }
        }
        release_x86 {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "x86"
            }
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix = '1'
            jniDebuggable = false
            debuggable = true
            resValue "string", "app_name", "V Assistant-debug"
            resValue "string", "base_acc_service_label", "V Assist基础DEBUG"
            resValue "string", "gesture_acc_service_label", "V Assist高级DEBUG"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    JPUSH_PKGNAME: "cn.vove7.vassistant.debug",
                    JPUSH_APPKEY : "2a428d87054dcc8b9fd45a30", //JPush 上注册的包名对应的 Appkey.
                    JPUSH_CHANNEL: "default", //暂时填写默认值即可.da
            ]
            ndk {//排除其他 abi
                abiFilters "armeabi-v7a", "x86"
            }
        }
//        beta {
//            versionNameSuffix = '-beta'
//        }
    }
    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        //最大堆内存
        javaMaxHeapSize "4g"
        //是否支持大工程模式
        jumboMode = true
        //预编译
        preDexLibraries = true
        //线程数
        threadCount = 8
    }
    productFlavors {
    }
    packagingOptions {
        exclude 'META-INF/proguard/coroutines.pro'
    }

    //配置打包apk路径及文件名
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
//            def type = "-" + buildType.name
            def filename = "../../apks/vassist-v${versionCode}.apk"
            outputFileName = filename
        }
    }

    packagingOptions {
        exclude 'META-INF/common_release.kotlin_module'
    }

}

task makeJar(type: Jar) {
    //指定生成的jar名
    baseName 'vassist-plugin-api2'
    //从哪里打包class文件
    from('build/tmp/kotlin-classes/debug/cn/vove7')
    //打包到jar后的目录结构
    into('cn/vove7/')
    //去掉不需要打包的目录和文件
    exclude('BuildConfig.class')
    //去掉R$开头的文件
    //exclude{ it.name.startsWith('R$');}
}
dependencies {
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0')

    implementation "androidx.appcompat:appcompat:$androidx_version"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'

    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation project(':executorengine')
    implementation project(path: ':common')
    implementation project(path: ':androlua')
    implementation project(path: ':rhino-android')
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:color:3.3.0'
    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    implementation 'org.adw.library:discrete-seekbar:1.0.1'
    implementation 'com.github.didikee:AndroidDonate:0.1.0'

    implementation 'com.github.Vove7:CodeView:1.0.2'
    implementation 'com.github.Vove7:BottomDialog:2.2.7'

    implementation 'com.github.Vove7:SlidePickerView:1.0.2'

    implementation 'com.github.android-notes:Cockroach:0.0.5'

    implementation 'com.github.KeepSafe:TapTargetView:1.13.0'
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'
    implementation 'com.github.Vove7:recycler-fast-scroll:0.3.3'

    implementation 'com.king.zxing:zxing-lite:1.1.1'
    implementation 'com.github.Vove7.MarkdownView:library:0.20.1'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'

    implementation project(path: ':speechLib')
    implementation project(':admin-manager')

    implementation "com.github.infotech-group:android-drawable-dsl:0.3.0"
    def work_version = "2.3.3"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    implementation 'com.google.android:flexbox:2.0.0'

}
repositories {
    flatDir {
        dirs 'libs'
    }
}
